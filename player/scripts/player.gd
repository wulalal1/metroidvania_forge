class_name Player extends CharacterBody2D

const DEBUG_JUMP_INDICATOR = preload("uid://dklsn8ip8frqg")

#region // signals
signal  damage_taken
#endregion

#region /// on ready variable
@onready var attack_sprite: Sprite2D = %AttackSprite2D
@onready var sprite: PlayerSprite = $Sprite2D
@onready var collision_stand: CollisionShape2D = $CollisionStand
@onready var collision_crouch: CollisionShape2D = $CollisionCrouch
@onready var animation_player: AnimationPlayer = $AnimationPlayer
@onready var one_way_platform_shape_cast: ShapeCast2D = $OneWayPlatformShapeCast
@onready var attack_area: AttackArea = %AttackArea
@onready var da_stand: CollisionShape2D = $DamageArea/DAStand
@onready var da_crouch: CollisionShape2D = $DamageArea/DACrouch
@onready var damage_area: DamageArea = %DamageArea


#endregion

#region /// player stats
var hp : float = 20 : 
	set(value) :
		hp = clamp(value,0,max_hp)
		Messages.player_healed_changed.emit(hp,max_hp)
var max_hp : float = 20 :
	set(value) :
		max_hp = value
		Messages.player_healed_changed.emit(hp,max_hp)
		
var dash : bool = true
var dash_count : int = 0
var double_jump : bool = true
var jump_count : int = 0
var ground_slam : bool =false
var morph_roll : bool = false

#endregion

#region /// export variable
@export var move_speed : float = 150
@export var max_fall_velocity : float = 600.0
#endregion
#region /// State Machine Variable
var states : Array[ PlayerState ]
var current_state : PlayerState :
	get : return states.front()
var previous_state : PlayerState :
	get : return states[ 1 ]
#endregion

#region /// State Machine Variable
var direction : Vector2 = Vector2.ZERO
var gravity : float = 980
var gravity_mulitplier : float = 1.0
#endregion

func _ready() -> void:
	#initialize states
	if get_tree().get_first_node_in_group("Player") != self:
		self.queue_free()
	initialize_states()
	self.call_deferred("reparent",get_tree().root)
	Messages.player_healed.connect(_on_player_healed)
	Messages.back_to_title_screen.connect(queue_free)
	damage_area.damae_taken.connect(_on_damage_taken)
	hp = max_hp
	pass
	
func  _unhandled_input(event: InputEvent) -> void:
	if event.is_action_released("jump") and  velocity.y < 0:
		velocity.y *= 0.5
	if event.is_action_pressed("action"):
		Messages.player_interacted.emit(self)
	elif event.is_action_pressed("pause"):
		get_tree().paused = true
		var pause_menu: PauseMenu = load("res://pause_menu/pause_menu.tscn").instantiate()
		add_child(pause_menu)
		return 
	change_state(current_state.handle_input(event))
	
	#debug
	if OS.is_debug_build():
		if event is InputEventKey and event.pressed:
			if event.keycode == KEY_MINUS:
				if Input.is_key_pressed(KEY_SHIFT):
					max_hp -= 10
				else:
					hp -= 2
			elif  event.keycode == KEY_EQUAL:
				if Input.is_key_pressed(KEY_SHIFT):
					max_hp += 10
				else:
					hp += 2
	#debug
	pass
	


func _process(_delta: float) -> void:
	update_direction()
	change_state(current_state.process( _delta ))
	pass


func _physics_process(_delta: float) -> void:
	velocity.y += gravity * _delta * gravity_mulitplier
	velocity.y = clampf(velocity.y,-1000.0,max_fall_velocity)
	move_and_slide()
	change_state(current_state.physics_process( _delta ))
	
	pass

func initialize_states() -> void:
	states = []
	#收集所有的初始化状态
	for c in $States.get_children():
		if c is PlayerState:
			states.append( c )
			c.player = self
		pass
	
	if states.size() == 0:
		return	
	
	#初始化所有状态
	for state in states:
		state.init()
	#设置第一个状态
	change_state( current_state )
	current_state.enter()
	$Label.text = current_state.name
	pass
	
func  change_state( new_state : PlayerState ) -> void:
	if new_state == null:
		return
	elif  new_state == current_state:
		return
	if current_state:
		current_state.exit()
		
	states.push_front( new_state )
	current_state.enter()
	states.resize(3)
	$Label.text = current_state.name
	pass

func update_direction() -> void:
	var prey_direction : Vector2 = direction
	#direction = Input.get_vector("left","right","up","down")
	var x_axis = Input.get_axis("left","right")
	var y_axis = Input.get_axis("up","down")
	direction =Vector2(x_axis,y_axis)
	
	if prey_direction.x != direction.x:
		attack_area.flip(direction.x)
		if direction.x < 0:
			sprite.flip_h = true
			attack_sprite.flip_h = true
			attack_sprite.position.x = -24
		elif direction.x > 0:
			sprite.flip_h = false
			attack_sprite.flip_h = false
			attack_sprite.position.x = 24
	#do the staff
	pass
	
	
	
func  add_debug_indicator( color : Color = Color.RED ) -> void:
	var d : Node2D = DEBUG_JUMP_INDICATOR.instantiate()
	get_tree().root.add_child( d )
	d.global_position = global_position
	d.modulate = color
	await get_tree().create_timer( 3.0 ).timeout
	d.queue_free()
	pass

func _on_player_healed(amount : float) -> void:
	hp += amount
	pass

func _on_damage_taken(attack_area : AttackArea) -> void:
	if current_state == PlayerStateDeath:
		return
	hp -= attack_area.damage
	damage_taken.emit()
	pass
func can_dash() -> bool:
	if dash == false or dash_count > 0:
		return false
	return true
