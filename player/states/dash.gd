class_name PlayerStateDash extends PlayerState
const DASH_AUDIO = preload("uid://8mnv3h3fu4tv")
@export var duration : float = 0.25
@export var speed : float = 300.0
@export var effect_delay : float = 0.05

var dir : float = 1.0
var time : float = 0.0
var effect_time : float = 0.0
@onready var damage_area: DamageArea = %DamageArea


func init() -> void:
	pass

#当我们进入这个状态会发生什么?
func enter() -> void:
	#play animation
	player.animation_player.play("dash")
	time = duration
	effect_time = 0.0
	#play animation
	get_dash_direction()
	damage_area.make_invulnerable(duration)
	Audio.play_spatial_sound(DASH_AUDIO,player.global_position)
	player.gravity_mulitplier = 0.0
	player.velocity.y = 0
	player.dash_count += 1
	player.sprite.tween_color(duration)
	pass

#当我们退出这个状态时会发生什么?
func exit() -> void:
	player.gravity_mulitplier = 1.0
	
	pass

#当按下按键处理情况会发生什么?
func handle_input( _event : InputEvent) -> PlayerState:
	#handle input
	if _event.is_action_pressed("action") and player.can_morph():
		return ball
	return null
	
	
func process(_delta: float) -> PlayerState:
	time -= _delta
	if time <= 0:
		if player.is_on_floor():
			return idle
		else:
			return fall
	
	effect_time -= _delta
	if effect_time < 0:
		effect_time = effect_delay
		player.sprite.ghost()
	return null

func physics_process(_delta: float) -> PlayerState:
	player.velocity.x = (speed*(time / duration) + speed) * dir
	return next_state

func get_dash_direction() -> void:
	dir = 1.0
	if player.sprite.flip_h == true:
		dir = -1.0
	pass
