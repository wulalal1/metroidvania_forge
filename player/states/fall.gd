class_name PlayerStateFall extends PlayerState

@onready var land_audio: AudioStreamPlayer2D = %LandAudio
@export var fall_gravity_mulitplier : float = 1.165
@export var coyote_time : float = 0.125
@export var jump_buffer_time : float = 0.2
var coyote_timer : float = 0
var buffer_timer : float = 0

func init() -> void:
	pass

#当我们进入这个状态会发生什么?
func enter() -> void:
	#play animation
	player.animation_player.play("jump")
	player.animation_player.pause()
	player.gravity_mulitplier = fall_gravity_mulitplier
	if player.jump_count == 0:
		player.jump_count = 1
	var prev : PlayerState = player.previous_state
	if prev == jump or prev == attack or prev == dash:
		coyote_timer = 0
	elif player.previous_state == crouch:
		coyote_timer = 0
		player.jump_count = 1
	else:
		coyote_timer = coyote_time
	pass

#当我们退出这个状态时会发生什么?
func exit() -> void:
	player.gravity_mulitplier = 1.0
	buffer_timer = 0 
	pass

#当按下按键处理情况会发生什么?
func handle_input( _event : InputEvent) -> PlayerState:
	#handle input
	if _event.is_action_pressed("dash") and player.can_dash():
		return dash
	if _event.is_action_pressed("attack"):
		if player.ground_slam and Input.is_action_pressed("down"):
			return ground_slam
		return attack
	if _event.is_action_pressed("jump"):
		if coyote_timer > 0:
			player.jump_count = 0
			return jump
		elif player.jump_count <= 1 and player.double_jump:
			return jump
			
		else:
			buffer_timer = jump_buffer_time
	return next_state
	
	
func process(_delta: float) -> PlayerState:
	coyote_timer -= _delta
	buffer_timer -= _delta
	set_jump_frame()
	return next_state

func physics_process(_delta: float) -> PlayerState:
	if player.is_on_floor():
		VisualEffects.land_dust(player.global_position)
		land_audio.play()
		#player.add_debug_indicator()
		#if buffer_timer > 0 and Input.is_action_pressed("jump"):
		if buffer_timer > 0:
			player.jump_count = 0
			return jump
		return idle
	player.velocity.x = player.direction.x * player.move_speed
	
	return next_state
	
func set_jump_frame() -> void:
	var frame : float = remap( player.velocity.y,0.0,player.max_fall_velocity,0.5,1.0)
	player.animation_player.seek(frame,true)
	pass
