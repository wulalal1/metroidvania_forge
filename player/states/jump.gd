class_name PlayerStateJump extends PlayerState

@export var jump_velocity : float = 450.0
@onready var jump_audio: AudioStreamPlayer2D = %JumpAudio


func init() -> void:
	pass

#当我们进入这个状态会发生什么?
func enter() -> void:
	#play animation
	if player.is_on_floor():
		VisualEffects.jump_dust(player.global_position)
	else:
		VisualEffects.hit_dust(player.global_position)
	player.animation_player.play("jump")
	player.animation_player.pause()
	#player.add_debug_indicator(Color.LIME_GREEN)
	do_jump()
	#check if this is a buffer jump
	if player.previous_state == fall and not Input.is_action_pressed("jump"):
		await get_tree().physics_frame
		player.velocity.y *= 0.5
		player.change_state(fall)
		pass
	pass

#当我们退出这个状态时会发生什么?
func exit() -> void:
	#player.add_debug_indicator(Color.YELLOW)
	pass

#当按下按键处理情况会发生什么?
func handle_input(event : InputEvent) -> PlayerState:
	#handle input
	if event.is_action_pressed("dash") and player.can_dash():
		return dash
	if event.is_action_pressed("attack"):
		if player.ground_slam and Input.is_action_pressed("down"):
			return ground_slam
		return attack
	if event.is_action_released("jump"):
		return fall
	return next_state
	
	
func process(_delta: float) -> PlayerState:
	set_jump_frame()
	return next_state

func physics_process(_delta: float) -> PlayerState:
	if player.is_on_floor():
		return idle
	elif player.velocity.y >= 0.0:
		return fall
	player.velocity.x = player.direction.x * player.move_speed
	return next_state

func do_jump() -> void:
	if player.jump_count > 0:
		if player.double_jump == false:
			return
		elif player.jump_count > 1:
			return
	player.jump_count += 1	
	player.velocity.y = -jump_velocity
	jump_audio.play()
	pass

func set_jump_frame() -> void:
	var frame : float = remap( player.velocity.y,-jump_velocity,0.0,0.0,0.5)
	player.animation_player.seek(frame,true)
	pass
	
